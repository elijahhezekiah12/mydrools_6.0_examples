/*
 * Copyright 2010 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.drools.tutorials.banking


/*
  Although we have separate rules for credits and debits, but we do not specify 
  a type when checking for earlier cashflows. This is so that all cashflows are 
  applied in date order, regardless of the cashflow type. In the conditions we 
  identify the account to work with, and in the consequences we update it with 
  the cashflow amount.
*/ 


/*
  The Credit Rule
  See Rule 3 for basic sorting rule example.
*/ 

rule "Rule 06 - Credit"  
    when
        $cashflow : AllocatedCashflow( $account : account, $date : date, $amount : amount, type==TypedCashflow.CREDIT )
        not AllocatedCashflow( account == $account, date < $date)
    then
        System.out.println("Credit: " + $date + " :: " + $amount);     
        $account.setBalance($account.getBalance()+$amount);
        System.out.println("Account: " + $account.getAccountNo() + " - new balance: " + $account.getBalance());          
        retract($cashflow);
end


/*
  The Debit Rule
  See Rule 3 for basic sorting rule example.
*/ 

rule "Rule 06 - Debit"  
    when
        $cashflow : AllocatedCashflow( $account : account, $date : date, $amount : amount, type==TypedCashflow.DEBIT )
        not AllocatedCashflow( account == $account, date < $date)
    then
        System.out.println("Debit: " + $date + " :: " + $amount);      
        $account.setBalance($account.getBalance() - $amount);
        System.out.println("Account: " + $account.getAccountNo() + " - new balance: " + $account.getBalance());           
        retract($cashflow);
end



